=title Bug in the for-loop of Perl? - B::Deparse to the rescue
=timestamp 2014-02-27T12:01:01
=indexes B::Deparse, for, foreach
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

The other day I got an email asking for my help. The reader had a short Perl snippet traversing two arrays.
One using the C-style <hl>for-loop</hl>, the other using Perl-style <hl>foreach-loop</hl>.

They acted differently.

A beginner would stare at it for a long time and not understand the problem. As he is not trusting
Perl yet, he might jump to the conclusion that there is a bug in Perl, or maybe that the
behavior of <hl>for</hl> has changed.

A seasoned Perl programmer would see the problem immediately. And he would be wrong.

Here is how you can find out what is the problem:

=abstract end

<h2>The code</h2>

<code lang="perl">
#!/usr/bin/perl

use strict;
use warnings;

my %hash = (
    'chr1' => [
        ['start','end','cat'],
        ['raj','end','cat']
    ],
    'chr2' => [
        ['start','end','cat'],
        ['start','end','cat']
    ]
);

print "Using C-ish for-loop syntax version\n";

foreach my $key (keys %hash) {
    my $j;
    my $i;
    print "$key: ";
    for ($i=0, $i < 2, ++$i ) {
        for ($j=0, $j<3, ++$j) {
             print "$hash{$key}[$i][$j]  ";
        }
    }
    print "\n";
}

print "\n\nUsing Perl-ish for-loop syntax version\n";

foreach my $key (keys %hash) {
    print "$key: ";
    for my $i (0..1) {
       for my $j (0..2) {
           print "$hash{$key}[$i][$j]  ";
       }
    }
    print "\n";
}
</code>

The output:

<code>
Using C-ish for-loop syntax version
chr1: end  end  end  end  end  end  end  end  end  
chr2: end  end  end  end  end  end  end  end  end  


Using Perl-ish for-loop syntax version
chr1: start  end  cat  raj  end  cat  
chr2: start  end  cat  start  end  cat 
</code>

When I got the script, first thing I checked if there is <a href="/strict">use strict;</a>
<hl>use warnings;</hl>. There were. Great!

The indentation also looked good. So what can cause he different behavior?

For a second I thought about maybe <hl>keys</hl> returning the values in different order in the two calls,
but that probably should not happen, and even if that was the case, the result should not be this.

Interestingly the author of this code used <hl>foreach</hl> for the outer loops and <hl>for</hl> in the
4 inner loops. Twice he was using it in C-style, twice he was using it Perl-style. I am not sure
if he was aware that <hl>for</hl> and <hl>foreach</hl> are synonyms, and Perl knows which one to used
based on the syntax.

Anyway, I still did not know what is the problem, but there was one small thing bothering me. The declaration of
<hl>$i<hl> and <hl>$j</hl> are outside of the <hl>for</hl> loop in the first case. let's fix that and have
the following as the first loop:

<code lang="perl">
foreach my $key (keys %hash) {
    print "$key: ";
    for (my $i=0, $i < 2, ++$i ) {
        for (my $j=0, $j<3, ++$j) {
             print "$hash{$key}[$i][$j]  ";
        }
    }
    print "\n";
}
</code>

Running the script now gave me this error:

<code>
$ perl for-each.pl 
Global symbol "$i" requires explicit package name at for-each.pl line 21.
Global symbol "$i" requires explicit package name at for-each.pl line 21.
Global symbol "$j" requires explicit package name at for-each.pl line 22.
Global symbol "$j" requires explicit package name at for-each.pl line 22.
Execution of for-each.pl aborted due to compilation errors.
</code>

What the ??? How did this happen?

I was quite frustrated by this time. You see I just woke up. Have not eaten anything yet.

Clearly Perl and I have a misunderstanding here.

So I wondered what does Perl think about this code.

<h2>B::Deparse - the magic wand</h2>

That's where I brought in the magic wand, aka. <a href="https://metacpan.org/pod/B::Deparse">B::Deparse</a>.
It can tell me what Perl thinks I wrote.


So I ran the original script using B::Deparse:
<hl>$ perl -MO=Deparse for-each.pl</hl>


<code lang="perl">
use warnings;
use strict;
my(%hash) = ('chr1', [['start', 'end', 'cat'], ['raj', 'end', 'cat']], 'chr2', [['start', 'end', 'cat'], ['start', 'end', 'cat']]);
print "Using C-ish for-loop syntax version\n";
foreach my $key (keys %hash) {
    my $i;
    my $j;
    print "${key}: ";
    foreach $_ ($i = 0, $i < 2, ++$i) {
        foreach $_ ($j = 0, $j < 3, ++$j) {
            print "$hash{$key}[$i][$j]  ";
        }
    }
    print "\n";
}
print "\n\nUsing Perl-ish for-loop syntax version\n";
foreach my $key (keys %hash) {
    print "${key}: ";
    foreach my $i (0 .. 1) {
        foreach my $j (0 .. 2) {
            print "$hash{$key}[$i][$j]  ";
        }
    }
    print "\n";
}
</code>

It replaced all for occurrences of <hl>for</hl> by <hl>foreach</hl>. That's strange. I would have expected it
to show the c-style for loops with the <hl>for</hl> keyword. Not only that, but if we take a closer look at
the first internal loop. It says  <hl>foreach $_ ($i = 0, $i < 2, ++$i)</hl>.
Why is it iterating over the <hl>$_</hl> the <a href="/the-default-variable-of-perl">default variable</a>?

That's bad. I thought we are iterating over <hl>$i</hl>. And we are using <hl>$i</hl> in the expression
printing the value of the <hl>%hash</hl>.

That's when the light came on. Perl thinks we had a Perl-style <hl>foreach</hl> loop there while we wanted a C-style
<hl>for</hl> loop.

The way Perl can differentiate between the two loops is that the Perl-style <hl>foreach</hl> loop has a list of values
in the parentheses, while the <a href="/for-loop-in-perl">C-style for loop</a> has 3 parts separated by <hl>;</hl>.

We had - by mistake - <hl>,</hl> (comma) separating the 3 parts of the <hl>for</hl> loop instead of <hl>;</hl>
(semicolon), and so Perl thought we meant a <hl>foreach</hl> loop iterating over the values
<hl>$i=0</hl>, <hl>$i < 2</hl>, and <hl>++$i</hl> instead of the C-style for loop.

A silly mistake, that causes this strange problem because of the duality of the for-loop in Perl.

<h2>The fixed version</h2>

The correct way to write those internal for-loops are as follows. Including the move of <hl>my</hl>
inside the for expression.

<code lang="perl">
foreach my $key (keys %hash) {
    print "$key: ";
    for (my $i=0; $i < 2; ++$i ) {
        for (my $j=0; $j<3; ++$j) {
             print "$hash{$key}[$i][$j]  ";
        }
    }
    print "\n";
}
</code>

<h2>Conclusion</h2>

There are cases when we have disagreement with Perl. 
<a href="https://metacpan.org/pod/B::Deparse">B::Deparse</a> can help us understand what
Perl though of our code.

