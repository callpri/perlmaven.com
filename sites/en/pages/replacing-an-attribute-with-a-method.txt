=title Replacing an accessor by a method (using BUILDARGS)
=timestamp 2015-04-10T22:50:00
=indexes BUILDARGS
=books moo
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

Back when we learned about <a href="/type-checking-with-moo">type checking with Moo</a> we had an example where
a Person had an age attribute. Unfortunately as time passes, the person represented by that object had a birthday
since then and so the age attribute does not reflect the correct number any more.

Probably it was not a good idea in the first place that we used <b>age</b> as an attribute as it basically changes every second.
Even if we only have birthdays once a year.

It would have been much better to have an attribute "birthdate" as that's something fixed.

But now it is already done. Our class is in use by lots of people around the world. We cannot just remove
the "age" attribute and add the "birthdate" attribute.

How can we fix this without breaking all the code out there?

=abstract end

Just to clarify, this is not a rare mistake. As we are always in a rush to release
the new version of our module/application whatever we always make such mistakes.
Even if not specifically with "age".

<h2>The origin</h2>

So we have this code in the Person.pm file:

<code lang="perl">
package Person;
use Moo;

has age => (is => 'rw');
has name => (is => 'rw');

1;
</code>

and we have the following code using it:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Person;

my $teacher = Person->new(name => 'Foo');
$teacher->age(20);
say $teacher->name;
say $teacher->age;

my $student = Person->new(age => 18, name => 'Bar');
say $student->age;
say $student->name;
</code>

Nothing special here.

<h2>Replace the attribute by a method</h2>

The age attribute was exposed to the outside world (people using the class) in two
places. One of them was the constructor where we could pass a value to the
age attribute, and the other one was the <hl>->age</hl> accessor.

In the first attempt we will make sure the accessor still works.

We will introduce a new attribute called <hl>birthdate</hl> which will be just a timestamp
returned by the <hl>time()</hl> function of perl,
and we will remove the <hl>age</hl> attribute.
We also add a new <a href="/methods-functions-and-subroutines-in-perl">method</a>
called <hl>age</hl> that will have two ways of operation.

If called with a number as the age of the person in years, the value will be converted
to the internal representation  (which is the current time less the year in seconds) and
stored it in the <hl>birthdate</hl> attribute.

If called without a parameter, then it will calculate the current age (in whole numbers)
as the difference between the current time and the <hl>birthdate</hl>.

Here is the new version of Person.pm:

<code lang="perl">
package Person;
use Moo;

has birthdate => (is => 'rw');
has name => (is => 'rw');

my $YEAR = 60*60*24*365;

sub age {
    my ($self, $age) = @_;

    if ($age) {
        $self->birthdate( time - $age * $YEAR );
    }

    return int( (time - $self->birthdate) / $YEAR );
}

1;
</code>

In order to simplify the example I used the variable <hl>$YEAR</hl> that holds an approximate number of seconds in a year.
This is good enough for our current example, though in a real application we would probably use a DateTime object there.

Let's see how well this works:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Person;

my $teacher = Person->new(name => 'Foo');
$teacher->age(20);
say $teacher->name;
say $teacher->age;
</code>

prints

<code>
Foo
20
</code>

which is what we expected.

We can even extend our code with a little experiment:

<code lang="perl">
my $teacher2 = Person->new(name => 'FooBar');
$teacher2->age(19.99999999);
say $teacher2->name;
say $teacher2->age;
sleep 5;
say $teacher2->age;
</code>

We set the age to be just before the age of 20, we wait and see how the returned age
(that we wanted to be a whole number) changes from 19 to 20:

<code>
FooBar
19
20
</code>

Last, but not least, we try to pass <hl>age</hl> in the constructor:

<code lang="perl">
my $student = Person->new(age => 18, name => 'Bar');
say $student->name;
say $student->age;
</code>

The output shows that the age attribute was swallowed by Moo and thus we get
a warning:

<code>
Bar
Use of uninitialized value in subtraction (-) at Person.pm line 16.
</code>

<h2>Fixing the constructor</h2>

In order to fix the constructor we are going to use the <hl>BUILDARGS</hl> method of Moo.

If you implement this method in your class, Moo will call it before calling the constructor.
It will get all the parameters the constructor gets (the name of the class and all the arguments
you passed to the <hl>->new</hl> call), and the method should return a reference to a hash
holding the (probably updated) parameters of the constructor.

When we call <hl>Person->new(name => 'Bar', age => 18)</hl>, perl would call the <hl>new</hl> method
with the following arguments: <hl>'Person', name => 'Bar', age => 18</hl>, which is just the same
a <hl>'Person', 'name', 'Bar', 'age', 18</hl>.
(If in doubt, see the explanation about the <a href="/perl-hashes">fat-arrow</a>.)

Moo already provides the <hl>new</hl> constructor, hence we don't have to call it.
Moo also check is we have implemented a method called <hl>BUILDARGS</hl>. If we have such function
in our module, then, before calling the <hl>new</hl> method, Moo will call this method.
So in our case <hl>$class</hl> will be 'Person' and <hl>%args</hl> will contain two key-value pairs:

<code lang="perl">
   'name' => 'Bar',
   'age' => 18
</code>

We remove the <hl>age</hl> and replace it with a <hl>birthdate</hl> key and with the appropriate value.
Then we return the reference to this hash.

Moo will grab that hash-reference and call <hl>new</hl> passing the name of the class ('Person') and the
new set of arguments that can be found in this hash reference.

Thus we could replace the 'age' argument passed in by the user, by the 'birthdate' argument.

<code lang="perl">
sub BUILDARGS {
    my ($class, %args) = @_;

    my $age = delete $args{age};
    if ($age) {
        $args{birthdate} = time - $age * $YEAR;
    }
    return \%args;
}
</code>

If you want to see it for yourself, add the following code to the Person.pm file:

<code lang="perl">
sub BUILDARGS {
    my ($class, %args) = @_;

    use Data::Dumper;
    print "Class: $class\n";
    warn Dumper \%args;

    my $age = delete $args{age};
    if ($age) {
        $args{birthdate} = time - $age * $YEAR;
    }
    return \%args;
}
</code>

and run the following script:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Person;

my $teacher = Person->new(name => 'Foo');
my $student = Person->new(age => 18, name => 'Bar');
</code>

The output will look like this:

<code>
Class: Person
$VAR1 = {
          'name' => 'Foo'
        };
Class: Person
$VAR1 = {
          'name' => 'Bar',
          'age' => 18
        };
</code>

You can see the name of the class was 'Person' in both cases and the
data received in %args matches what we passed to the <hl>new</hl> call.


<h2>Put it together and test it</h2>

The whole Person.pm file:

<code lang="perl">
package Person;
use Moo;

has birthdate => (is => 'rw');
has name => (is => 'rw');

my $YEAR = 60*60*24*365;

sub BUILDARGS {
    my ($class, %args) = @_;
    my $age = delete $args{age};
    if ($age) {
        $args{birthdate} = time - $age * $YEAR;
    }
    return \%args;
}

sub age {
    my ($self, $age) = @_;

    if ($age) {
        $self->birthdate( time - $age * $YEAR );
    }

    return int( (time - $self->birthdate) / $YEAR );
}

1;
</code>

The script:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Person;

my $teacher = Person->new(name => 'Foo');
$teacher->age(20);
say $teacher->name;
say $teacher->age;

my $student = Person->new(age => 18, name => 'Bar');
say $student->name;
say $student->age;
</code>

The output:

<code>
Foo
20
Bar
18
</code>

<h2>Conclusion</h2>

So we changed the internal representation of <b>age</b> replacing the <b>age</b> attribute by a <b>birthdate</b> attribute.
The users of our class don't need to change anything as the retained the old API.

We could now decide to deprecate the old API but keep it around for a while to let our users adjust their code,
or we can decide to keep it around forever.


