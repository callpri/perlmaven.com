=title How to download a Perl module from CPAN
=timestamp 2015-03-14T02:30:01
=indexes CPAN, WWW::Mechanize
=status show
=author szabgab
=archive 1
=comments_disqus_enable 0

=abstract start

Normally if you want to <a href="/how-to-install-a-perl-module-from-cpan">install a Perl module from CPAN</a> you don't need
to manually download it, as there are clients for CPAN that will do it for you.

However when I research a module, for example to write an article about it, or to see how another module is using it, I often prefer to have
the whole distribution on my disk. That way I have the tests included with the distribution and, and if it contains examples
I have those too.

=abstract end

Let's say I'd like to download the distribution that contains the <a href="https://metacpan.org/pod/WWW::Mechanize">WWW::Mechanize</a> module.


<h2>cpanm</h2>

If you have <a href="http://cpanmin.us">CPAN Minus</a> installed you can type in <hl>cpanm --look WWW::Mechanize</hl>. It will download the distribution,
unzip it and open a subshell in the unzipped directory. That's cool, but in many cases I'd need to have several distributions to be around, and
I don't really like that subshell. I'd like the downloaded and unzipped directory to be easily accessible later on from my regular shell.

<h2>cpan</h2>

As pointed out by dnmfarrell on <a href="http://www.reddit.com/r/perl/comments/2yzuf4/how_to_download_a_perl_module_from_cpan/">Reddit</a>,
the <hl>cpan</hl> command, which is the regular cpan client also has a usefule option.
<hl>cpan -g WWW::Mechanize</hl> would download the zip file of the latest distribution providing the WWW::Mechanize module and
would save it in the current directory. I would still need to unzip it, but this is also a great solution.

There might be some bug using this feature on a newly configured cpan client as I've reported <a href="https://rt.cpan.org/Ticket/Display.html?id=102778">here</a>,
but I think if you regularily use this cpan client then it will work fine.

<h2>git-cpan</h2>

Then there is the <a href="https://metacpan.org/pod/distribution/Git-CPAN-Patch/bin/git-cpan">git-cpan</a> command line tool
that comes with <a href="https://metacpan.org/pod/Git::CPAN::Patch">Git::CPAN::Patch</a>.
It seems to be everything I could want and more. It would fetch a distribution from CPAN, create a local Git repository
and let you hack on the code.

I tried <hl>git-cpan clone WWW::Mechanize</hl>. It recognized that WWW::Mechanize already has a repository on GitHub,
and cloned that repository. Unfortunately when I tried to run <hl>git-cpan clone XML::DT</hl> (a module that does not
declare its repository), I got several errors. I have <a href="https://github.com/yanick/git-cpan-patch/issues/20">reported the issue</a>.

<h2>Using WWW::Mechanize</h2>

My main issue though is that I wanted something simple. So here is what I wrote:

<include file="examples/download-cpan.pl">

A very simple and probably fragile solution.

The script accepts a URL on the command line. One that either leads to a module on MetaCPAN, such as this:
<hl>https://metacpan.org/pod/WWW::Mechanize</hl> or one that leads to a distribution. Such as this:
<hl>https://metacpan.org/release/WWW-Mechanize</hl>.

It looks for the link that says <b>Download ...</b>, take the URL where that link leads. Downloads the thing behind the link,
saves it to the <b>/tmp</b> directory an unzips it.

The script uses <a href="https://metacpan.org/pod/WWW::Mechanize">WWW::Mechanize</a> module to fetch the HTML page of MetaCPAN

Get the parameter from the command line. Exit with an error message if there was not parameter on the command line.:

<code lang="perl">
my $url = shift or die "Usage: $0 URL\n";
</code>

Check if the given parameter is in the format of either of the pages mentioned above and extract the name of the
module or distribution into the <hl>$name</hl> variable. Both regexes start by matching a URL on the MetaCPAN
site and then containing letters, numbers and some extra characters.


<code lang="perl">
my $name;
if ($url =~ m{^https://metacpan.org/pod/([a-zA-Z0-9:]+)$}) {
    $name = $1;
} elsif ($url =~ m{^https://metacpan.org/release/([a-zA-Z0-9-]+)$}) {
    $name = $1;
}
</code>

If <hl>$name</hl> is empty, exit the script with an error message. This was not one of the recognized URL formats:

<code lang="perl">
die "Invalid URL\n" if not $name;
</code>


Create the WWW::Mechanzie object and fetch the URL the user gave us:

<code lang="perl">
my $w = WWW::Mechanize->new;
$w->get($url);
</code>

On the downloaded page try to find a link that matches the regex <hl>^Download</hl>. That is a link that starts with the word <hl>Download</hl>
Exit the script with an error message if no such link could be found:

<code lang="perl">
my $download_link = $w->find_link( text_regex => qr{^Download} );
die "Could not find download link\n" if not $download_link;
</code>

The value returned by the <hl>find_link</hl> method is either <a href="/undef-and-defined-in-perl">undef</a>, if no link was found,
or an instance of <a href="https://metacpan.org/pod/WWW::Mechanize::Link">WWW::Mechanize::Link</a>.

From the object we can extract the URL of the link using the <hl>url</hl> method and then using a regular expression we extract
the last part of the string. The regex itself will match <hl>[^/]</hl> (any character except slash, till the end of the string.
That is it will match the name of the file at the end of the URL:

<code lang="perl">
my ($file) = $download_link->url =~ m{([^/]+)$};
say $download_link->url;
say $file;
</code>


From the filename and from the <hl>$dir</hl> variable we declared at the beginning of the script we create a local path
where we would like to save the downloaded zip file. We check if the file already exists and exit the script if the file is there.
Apparently we have already downloaded this version of this  distribution:

<code lang="perl">
my $path = "$dir/$file";
if (-e $path) {
    say "Already downloaded to $path";
    exit;
}
</code>


The <hl>follow_link</hl> method will search for the link again and click on it. Effectively downloading the content of the file
but keeping it in memory as the <hl>content</hl> of the page.

<code lang="perl">
$w->follow_link( text_regex => qr{^Download} );
</code>

The <hl>save_content</hl> method will save the content of the current page which should be the content of the content of the zip file.
Still zipped. In the <hl>$path</hl> variable we provide the local path where the content should be save and we also tell it to save the
content as a binary file. After all we are talking about a zip file.


<code lang="perl">
$w->save_content( $path, binary => 1 );
</code>

Once that's done, we change to the directory where we saved this file and call the external <hl>tar</hl> command to unzip the file.

<code lang="perl">
chdir $dir;
system "tar xzf $file";
</code>

A rather simple use of the <a href="https://metacpan.org/pod/WWW::Mechanize">WWW::Mechanize</a> module.

