=title $, the output field separator, and $" the list separator of Perl
=timestamp 2014-02-13T09:30:01
=indexes $LIST_SEPARATOR, $", $OUTPUT_FIELD_SEPARATOR, $OFS, $,
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

Perl has a number of "magic" variables. Some are really useful, some are there mostly for historical reasons.
This time we are going to look at two: <hl>$,</hl> also known as <b>output field separator</b>, and
<hl>$"</hl> also known as <b>list separator</b>.

=abstract end

Assuming this at the beginning of our file:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;
use 5.010;

my @names = qw(Foo Bar Que);
</code>

<h2>$, the Output field separator</h2>

When printing a list of values or an array, Perl automatically prints the content of <hl>$,</hl>,
which is <hl>undef</hl> by default, between every two values.

So <hl>say 'A', 'B', 'C';</hl> will print <hl>ABC</hl>

and <hl>say @names;</hl> will print <hl>FooBarQue</hl>

One can change <hl>$,</hl> to any string

<code lang="perl">
$, = '--';
say 'A', 'B', 'C';  # A--B--C
say @names;         # Foo--Bar--Que
</code>

but it is really not advised! Especially because <hl>$,</hl> is global to
the whole process so if we change it in one location, it changes to the
rest of the code. Even in other modules. (Unless we localize it using
the <hl>local</l> statement.)

Using <hl>join</hl> is much clearer and has the same result:

<code lang="perl">
say join '--', 'A', 'B', 'C';
say join '--', @names;
</code>

In really serious cases, I'd probably create a function, rather than change <hl>$,</hl>:

<code lang="perl">
sub mysay {
    say join '--', @_, 
}

mysay 'A', 'B', 'C';
mysay @names;
</code>

It is much more readable.

The way to remember the variable is that it "replaces commas in a print statement".

If using the <a href="https://metacpan.org/pod/English">English</a> module, <hl>$,</hl> is also
called <hl>$OUTPUT_FIELD_SEPARATOR</hl> or  <hl>$OFS</hl>.

<h2>$" the list separator</h2>

Similarly, when we embed an array in a double-quoted string, the expression will return
the values of the array separated by the content of <hl>$"</hl>, which by default is a single space.

<hl>say "@names";</hl> will print <hl>Foo Bar Que</hl>.

Here too we can change the content of <hl>$"</hl>:

<code lang="perl">
$" = '-+-';
say "@names";     # Foo-+-Bar-+-Que
</code>

or we could use <hl>join</hl> instead:

<code lang="perl">
say join '-+-', @names;     # Foo-+-Bar-+-Que
</code>

or create a function:

<code lang="perl">
sub mysay {
    say join '-+-', @_, 
}

mysay @names;    # Foo-+-Bar-+-Que
</code>

When using the English module, the variable <hl>$"</hl> is also called <hl>$LIST_SEPARATOR</hl>.

But, let me repeat, I would not recommend changing it.

<h2>Finding the violators using Perl::Critic</h2>

So if I don't recommend using it, how can I make sure neither my code, nor the code of my fellow co-workers
change this variable? 

<a href="https://metacpan.org/pod/Perl::Critic">Perl::Critic</a> has a policy called
<a href="https://metacpan.org/pod/Perl::Critic::Policy::Variables::ProhibitPunctuationVars">Variables::ProhibitPunctuationVars</a>
that will catch the use of these variables in your code. You can try to find them using
<hl>perlcritic --single-policy Variables::ProhibitPunctuationVars script.pl</hl> that will give you a report like this:
<b>Magic punctuation variable $, used at line 9, column 1.  See page 79 of PBP.  (Severity: 2)</b>

You could also set up an automated test using <a href="https://metacpan.org/pod/Test::Perl::Critic">Test::Perl::Critic</a>.

See how you could <a href="/how-to-improve-my-perl-program">improve your code</a> using Perl::Critic <a href="/perl-critic-one-policy">one policy at a time</a>.

<h2>Localizing $" and $,</h2>

While I still not recommend you to change the variables, but if you really have to, or you find a piece of code that does it and you
want to make sure the change does not have a global impact you can localize the change:


<code lang="perl">
say "@names";           # Foo Bar Que
{
    local $" = '-+-';   # Foo-+-Bar-+-Que
    say "@names";
}
say "@names";           # Foo Bar Que
</code>


