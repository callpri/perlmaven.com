=title Constructor and accessors in classic Perl OOP
=timestamp 2014-01-29T08:30:01
=indexes bless, package, @_, $_[0]
=status show
=books advanced
=author szabgab
=comments_disqus_enable 0

=abstract start

While for new applications it would be recommended to use <a href="/moo">Moo</a> or <a href="/moose">Moose</a> for writing Object Oriented Perl code,
sometimes you cannot use them. For example when you maintain an old code base, or when your management does not let you install modules
form CPAN.

=abstract end

We'll create a simple class representing a date. (Year, Month, Day).

We call our class "My::Date". It is usually recommended to put each class in its own file and to keep all the files
implementing modules or classes in the lib directory. As the <hl>::</hl> separator is mapped to subdirectories, the
code of the <hl>My::Date</hl> class should be placed in the <hl>lib/My/Date.pm</pl> file of the project.

(aka "best practices")

<code lang="perl">
package My::Date;
use strict;
use warnings;

sub new {
    my ($class, %args) = @_;
    return bless \%args, $class;
}

1;
</code>

The code of the script using it should go in the </hl>scripts/</hl> or <hl>bin/</hl> subdirectory of the project.
In our case it is the file <hl>bin/date.pl</hl> and contains the following:

<code lang="perl">
use strict;
use warnings;

use FindBin;
use File::Spec;
use lib File::Spec->catdir($FindBin::Bin, '..', 'lib');

use My::Date;

my $d = My::Date->new(year => 2013, month => 1, day => 27);
say $d;
</code>

The directory of the project looks like this:

<code>
$ tree
.
├── bin
│   └── date.pl
└── lib
    └── My
        └── Date.pm
</code>

First the basic boiler-plate of <a href="/strict">use strict</a>, and <hl>use warnings</hl> and
the declaration of the minimal Perl version. (The only reason this code requires 5.10 is so that I can use the
<hl>say</hl> function. Otherwise 5.6 could be used as well.)

Then we <a href="/how-to-add-a-relative-directory-to-inc">change @INC to a directory relative to the script</a>,
and we load the <hl>My::Date</hl> module that contains our class definition.

A class in Perl is just a module with slightly different behavior.

Then we called the constructor (the <hl>new</hl> method) and printed the content of the returned value.

<code>
My::Date=HASH(0x7fea348eb300)
</code>

So far the usage was simple. The class definition itself starts with the <hl>package</hl> keyword that
declares a new name-space. The <hl>1;</hl> at the end of the file is not very interesting. There needs to be
a <a href="/boolean-values-in-perl">true</a> value there.

The interesting part is the <hl>new</hl> method. While we could call our constructor any name, the accepted best
practice is to use the name <hl>new</hl>. When the user calls <hl>My::Date->new()</hl> perl will call the
<hl>new</hl> function in the <hl>My::Date</hl> package passing the name of the class "My::Date" as the first parameter.
Before all the parameters the user passed to the constructor.

Hence <hl>$class</hl> will hold "My::Date" and <hl>%args</hl> will capture all the key-value pairs passed to the constructor.

The call to <hl>bless</hl> associates the hash-reference to <hl>%args</hl> with the name-space passed in the <hl>$class</hl> variable
and returns the already blessed reference. This is returned to the caller. This is the object. (Or instance.)

When the user printed <hl>$d</hl>, she printed this blessed reference. That's why the output contains <hl>My::Date=</hl> in addition
to the address of the hash reference.

<h2>Attributes</h2>

In all this code no attributes were mentioned.
The keys passed to the constructor will be kept as keys in the blessed hash-reference. These are the attributes of the object.
Perl, in this classic way of Object Oriented code does not provide any mechanism to check if all the required attributes were passed.
If the values of the attributes were valid, or if there were additional attributes. There are tools to implement these, but for
now we accept the classic behavior that will turn every key-value pair into attributes.


<h2>Accessors: Getters, setters</h2>

Having a constructor create an object is great, turning every parameter into an attribute without any check is lazy,
but we would like to be able to retrieve and maybe even to change the content of the attributes.

For this, in classic Perl, we have to implement our getters and setters:

This code, as part of the Date.pm file implements the getters/setters for the 3 attributes we plan to handle:

<code lang="perl">
sub year {
    my ($self, $value) = @_;
    if (@_ == 2) {
        $self->{year} = $value;
    }
    return $self->{year};
}

sub month {
    if (@_ == 2) {
        $_[0]->{month} = $_[1];
    }
    return $_[0]->{month};
}

sub day {
    return $_[0]->{day} = @_ == 2 ? $_[1] : $_[0]->{day};
}
</code>

When the user calls <hl>$d->year(1001)</hl> perl first checks what is in <hl>$d</hl>. In our case there
is a reference <hl>blessed</hl> into the <hl>My::Date</hl> namespace. Therefor, perl will look for the
<hl>year</hl> function in the <hl>My::Date</hl> package. If not found it will throw an exception:

<hl>Can't locate object method "year" via package "My::Date" at bin/date.pl line 28</hl>

If it finds the function, it will call it passing <hl>$d</hl> as the first parameter, before any
parameters passed by the user. In our case <hl>$d</hl> will arrive in the variable <hl>$self</hl>,
and 1001 will arrive in <hl>$value</hl>.

The name <hl>$self</hl> is not reserved, but most Perl programmers will use this name to contain
the current instance. (This is similar to "self" in Python and "this" in Java.)

As the object, hold in <hl>$self</hl> is a reference to a hash, and the attributes are just keys in this hash,
we can access the "year" attribute using <hl>$self->{year}</hl>.
Because in this example we wanted to use the same function (<hl>year</hl>) to be both the setter and the getter,
we need to first check if the user has passes 2 arguments - meaning the user want to set the attribute. In
that case we assign the <hl>$value</hl> to the <hl>$self->{year}</hl> attribute.

In any we return the content of <hl>$self->{year}</hl>. That is, this accessor will always return the content
of the attribute, but if it gets two parameters it will first set it to this new value.

The other two functions, <hl>month</hl> and <hl>day</hl> do exactly the same, they just don't use temporary variables.

This code will use the new accessors:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use FindBin;
use File::Spec;
use lib File::Spec->catdir($FindBin::Bin, '..', 'lib');
use My::Date;

my $d = My::Date->new(year => 2013, month => 1, day => 27);
say $d;
say $d->year;
say $d->month;
say $d->day;

say '';
say $d->year(1001);
say $d->year;

say '';
say $d->month(12);
say $d->month;

say '';
say $d->day(9);
say $d->day;
</code>

<code lang="perl">
</code>

