=title How to set default values in Perl
=timestamp 2014-12-17T09:30:01
=indexes //, //=, ||, ||=
=status show
=author szabgab
=comments_disqus_enable 0


=abstract start

Often there are cases your Perl application has some default values that can be overridden
by a value in a configuration file or on the command line. The same can be the case
for individual functions in perl that accept arguments. In case the arguments
are not provided, the function will use its default values.

Let's see how to set default values.

=abstract end

<h2>Scalars</h2>

The common way to set a default value is using the <hl>//</hl>
(the defined or operator that was <a href="/what-is-new-in-perl-5.10--say-defined-or-state">introduced in perl 5.10</a>):

<code lang="perl">
sub f {
    my $count = shift // $SOME_DEFAULT;
}
</code>

In another way it would be:

<code lang="perl">
sub f {
    my ($count, $other_param) = @_;
    $count //= $SOME_DEFAULT;
}
</code>

Similarly when accepting values from the command line one would write:

<code lang="perl">
my $filename = shift // 'default_file';
</code>


<h2>Perl older than 5.10</h2>

If you have to make sure your code runs on perl versions lower than 5.10 you can use the following code:

<code lang="perl">
sub f {
    my $count = shift;
    $count = $SOME_DEFAULT if not defined $count;
}
</code>

or use the <a href="/the-ternary-operator-in-perl">ternary operator</a>:

<code lang="perl">
sub f {
    my ($count, $param) = @_;
    $count = defined $count ? $count : $SOME_DEFAULT;
}
</code>

In some places you'll see people using <hl>||</hl>, the regular or operator, to set default values as in the following example:

<code lang="perl">
sub f {
    my $count = shift || $SOME_DEFAULT;
}
</code>

This works too, in most of the cases but if an empty string, or the number 0 are valid parameters of the function this
will be incorrect. This will override those, otherwise valid values with the value in <hl>$SOME_DEFAULT</hl>
That's because both the empty string and the number 0 are evaluated to <a href="/boolean-values-in-perl">false</a>.

<h2>Arrays</h2>

I don't think I have seen code where individual element of an array were set as defaults, and I don't think there is a nice
way to do that besides treating them as individual scalar values. It is also not often that you would pass an array with "hole"
in it. It is much more common to pass an array reference, and in that case, if the user has not passed anything there, then
it might be interesting to set it to a default value.

But that case is just a special case of the default scalar values as discussed above except that <hl>$SOME_DEFAULT</hl> will
be a reference to an array. Possibly an empty array:

<code lang="perl">
f('Foo', ['Bar', 'Baz']);

sub f {
   my ($name, $friends) = @_;
   $friends //= [];
}
</code>


<h2>Hashes</h2>

It much more often that we can see a function accepting key-value pairs that are assigned to a hash, or a reference to a hash.
In some cases you'd want to set certain default values to some of the keys.

<code lang="perl">
use strict;
use warnings;
use Data::Dumper qw(Dumper);

f(cmd => 'send', to => 'near-by-address');
f(cmd => 'copy');

sub f {
    my %default = (
        from  => 'local-machine',
        to    => 'remote-machine',
    );

    my %params = (%default, @_);

    print Dumper \%params;
}
</code>

In this script we created a hash called <hl>%default</hl> with some key-value pairs in it. Then we assigned to the
<hl>%params</hl> the mix of <hl>(%default, @_)</hl>.  The result looks like this:

<code>
$VAR1 = {
          'cmd' => 'send',
          'to' => 'near-by-address',
          'from' => 'local-machine'
        };
$VAR1 = {
          'cmd' => 'copy',
          'from' => 'local-machine',
          'to' => 'remote-machine'
        };
</code>

In the first call we supplied values for both the 'cmd' and the 'to' fields. In that case the <hl>%params</hl> hash got the values
of 'cmd' and 'to' from the calling of the function, while the value if 'from' is the one that was in the <hl>%default</hl> hash.

In the second call we only supplied value for the 'cmd' key and thus the result was that <hl>%params</hl> got the values of both
'to' and 'from '  from the <hl>%default</hl> hash.

The reason this work is because when we put the <hl>%default</hl> array in parentheses, perl flattened the content so in effect this is what we had
at the first call:

<code>
   my %params = (
        from  => 'local-machine',
        to    => 'remote-machine',
        cmd   => 'send',
        to    => 'near-by-address'
   );
</code>

Note, we effectively supplied the 'to' key twice. Whenever we do that the last value supplied overrides any previous value so the effect was
this assignment:

<code>
   my %params = (
        from  => 'local-machine',
        cmd   => 'send',
        to    => 'near-by-address'
   );
</code>

