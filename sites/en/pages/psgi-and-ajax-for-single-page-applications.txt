=title PSGI and AJAX for single-page applications
=timestamp 2015-06-29T14:30:01
=indexes PSGI, Ajax, JavaScript
=status show
=books psgi
=author szabgab
=comments_disqus_enable 0

=abstract start

In the "old school" web applications and in the previous <a href="/psgi">PSGI examples</a> every click on a button caused the
the browser to load a whole new page. In more modern web applications the browser only loads a single HTML page that has
a lot of JavaScript in it. Then on every click some JavaScript code runs. It sends an
<a href="http://en.wikipedia.org/wiki/Ajax_(programming)">AJAX</a> request to the server in the background. Asynchronously.
When the reply arrives, another JavaScript function runs that updates the site.

As there is effectively only only one page which is constantly changing, these kind of applications are
called <a href="http://en.wikipedia.org/wiki/Single-page_application">Single-page application</a>.

=abstract end

In this example we'll see a very simple application, the good old "echo", but implemented with some low-level tools.
We are going to use <a href="/psgi">PSGI</a> on the server side and plain JavaScript on the front-end. 

We save the following code in the <hl>echo_ajax.psgi</hl> file and run it using <hl>plackup echo_ajax.psgi</hl>.
Visiting <hl>http://localhost:5000/</hl> will show an entry box and a button. If we type in something and click on
the button the, text will be displayed under the horizontal line after being sent to the server and back.
The URL itself will not change and the page won't be reloaded.

Behind the scenes, when we load the main page, it also loads a few lines of JavaScript. When we click the button
a JavaScript function runs, takes the value from the entry box and sends a request to the server. The request is
exactly the same as the URL was in the previous case. For example if we typed in "hello" the JavaScript will
send a request for <hl>http://localhost:5000/echo?field=hello</hl>. It won't be displayed as this happens in the background.
It also happens asynchronously, which means the calling function returns immediately, and when the response arrives
another function will be called.

In response to the above request the server will return a string containing a <a href="http://json.org/">JSON</a> data structure.
In case you have not encountered it, JSON is basically the name of all the data structures in JavaScript. As if there was a single word
for "hash or array" in perl. I guess "Complex data structure" might be a good candidate.
The point is, that we can easily generate such string from our arrays and hashes in Perl, and in JavaScript it is
just a native data structure.

<h2>The back-end</h2>

The back-end code, the part that runs on the server, the part that is written in Perl, uses the same routing as we have already
seen in <a href="/routing-with-psgi">routing and PSGI</a> though there is one substantial difference.
The <hl>serve_echo</hl> function instead of returning and HTML, it will return a data structure in JSON format.

Instead of creating HTML, the function fills the variable <hl>$data</hl> with a hash reference.
<hl>{ txt => 'You said: ' . $request->param('field') };</hl> or if no value was supplied
then <hl>{ txt => 'You did not say anything.' };</hl>.

Then the function returns the 3 element array reference as earlier, but the content type is
now set to be <hl>application/json</hl>, after all we are returning JSON and not HTML,
and the string returned is the JSON string created by the <hl>to_json</hl> function
imported from the <a href="https://metacpan.org/pod/JSON">JSON</a> module.

The <hl>serve_root</hl> function remained the same, just the content it returns changed.


<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use Plack::Request;
use JSON qw(to_json);

my %ROUTING = (
    '/'      => \&serve_root,
    '/echo'  => \&serve_echo,
);


my $app = sub {
    my $env = shift;
 
    my $request = Plack::Request->new($env);
    my $route = $ROUTING{$request->path_info};
    if ($route) {
        return $route->($env);
    }
    return [
        '404',
        [ 'Content-Type' => 'text/html' ],
        [ '404 Not Found' ],
    ];
};

sub serve_root {
    my $html = get_html();
    return [
        '200',
        [ 'Content-Type' => 'text/html' ],
        [ $html ],
    ];
} 

sub serve_echo {
    my $env = shift;
 
    my $request = Plack::Request->new($env);
    my $data;
    if ($request->param('field')) {
        $data = { txt => 'You said: ' . $request->param('field') };
    } else {
        $data = { txt => 'You did not say anything.' };
    }
    return [
        '200',
        [ 'Content-Type' => 'application/json' ],
        [ to_json $data ],
    ];
}


sub get_html {
    return q{
      <input id="field">
      <button id="echo">Echo</button>
      <hr>
      <div id="response"></div>

      <script>
      function show_response(resp) {
          document.getElementById('response').innerHTML = resp['txt'];
      }

      function send_text() {
         ajax_get_json('/echo?field=' + document.getElementById('field').value, show_response);
      }

      function ajax_get_json(url, on_success) {
          var xmlhttp = new XMLHttpRequest();
          xmlhttp.onreadystatechange = function() {
              if (xmlhttp.readyState == 4 && xmlhttp.status == 200) {
                  console.log('responseText:' + xmlhttp.responseText);
                  on_success(JSON.parse(xmlhttp.responseText));
              }
          }
          xmlhttp.open("GET", url, true);
          xmlhttp.send();
      }

      document.getElementById('echo').addEventListener('click', send_text);
      </script>

    }
}
</code>


<h2>The front-end</h2>

The <hl>get_html</hl> function will return the HTML. In a real application this would probably read it from an
external template file, but for a small example this works as well. Returned content consists of some HTML and some JavaScript.
If we compare it to the HTML that was returned in the <a href="/routing-with-psgi">previous example</a>
then these are the changes:

In the HTML we removed the <hl>form</hl> tag as we don't really need that functionality any more. We kept the input field,
but instead of having a <hl>name</hl> attribute, it now has and <hl>id</hl> attribute. It will make it easier for the JavaScript
code to find this element and extract its value. The button was also changed form an <hl>input</hl> element
of type <hl>button</hl> to a <hl>button</hl> element. We also added an empty <hl>div</hl> element with an <hl>id="response"</hl>.
That's where we are going to write the response we get from the server. That's all the HTML.

We also embedded the JavaScript code in the HTML page using the <hl>script</hl> tags. In a real application we would probably put
all the JavaScript in a separate file, but again for such a small example we can get away with the sloppiness.

There are 3 JavaScript function, defined. The <hl>show_response</hl> and the <hl>send_text</hl> are relevant to our
example. The <hl>ajax_get_json</hl> is just a generic function that could be reused in any other example. It gets two parameters.
The first is a URL, the second is a function. It sends a GET request to the URL and makes sure that when the response arrives
the function that was the second parameter will be called. We won't go into more details. It is a reasonable, albeit not full solution.

Once the page gets loaded in the browser the last line of the <hl>script</hl> section will be executed:

<code>
document.getElementById('echo').addEventListener('click', send_text);
</code>

This will locate the HTML element that has <hl>id="echo"</hl> (which is the <hl>button</hl>) and attaches a call-back to the
<hl>click</hl> event of that element. In other words, when the user clicks on the button, the function <hl>send_text</hl>
will be called.

If we look at the <hl>send_text</hl> function, it calls the <hl>ajax_get_json</hl> mentioned earlier with a URL
and with <hl>show_response</hl> as the second parameter. The first parameter, the URL, is created by the concatenation of
<hl>/echo?field=</hl> and the value of the element with <hl>id="field"</hl> using this expression:
<hl>document.getElementById('field').value</hl>
Thats' the whole "sending the request" part.

When the response arrives from the server, the <hl>show_response</hl> function is called (this was arranged by the
<hl>ajax_get_json</hl> helper function) and the JSON data structure sent by the server will be passed as a parameter <hl>resp</hl>.

On the server side we arranged a hash to be sent with a key 'text'. In the client side, in the <hl>show_response</hl>
function we can access the value of this key by using <hl>resp['txt']</hl>.

The expression <hl>document.getElementById('response').innerHTML = resp['txt'];</hl> will locate the element with <hl>id="response"</hl>
and fill its content with the text that was send from the server. That's all we want.


<h2>Conclusion</h2>

This was a small example and some parts were missing, but it can provide you the basic understanding how AJAX works,
and how can it be used with PSGI as the back-end.

