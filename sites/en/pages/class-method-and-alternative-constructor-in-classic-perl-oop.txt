=title Class method and alternative constructor in classic Perl OOP
=timestamp 2014-02-06T13:30:01
=indexes blessed, Scalar::Util
=status show
=books advanced
=author szabgab
=comments_disqus_enable 0

=abstract start

In the first article we created
a <a href="/constructor-and-accessors-in-classic-perl-oop">constructor called new</a> that accepted
a set of key-value pairs.

What if e would like to provide a user with an alternative way to instantiate an object?
For example we would like to accept strings like '2013-11-23'?

=abstract end

We could somehow change the <hl>new</hl> method to notice when it only receives a single string
and handle that case separately, but moving this into a separate method provides an opportunity
to write cleaner code.

This is all the code we have to add to our Date.pm file from the <a href="/stringification-in-classic-perl-oop">example with stringification</a>.

<code lang="perl">
# YYYY-MM-DD
sub new_from_string {
    my ($class, $date_string) = @_;

    my ($year, $month, $day) = split /-/, $date_string;
    return $class->new(year => $year, month => $month, day => $day);
}
</code>

Then we can write the following code:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use FindBin;
use File::Spec;
use lib File::Spec->catdir($FindBin::Bin, '..', 'lib');
use My::Date;

my $d1 = My::Date->new(year => 2013, month => 1, day => 27);
say $d1;

my $d2 = My::Date->new_from_string('2013-1-28');
say $d2;
</code>

and we get the output:

<code>
Date(2013, 1, 27)
Date(2013, 1, 28)
</code>

<h2>Class method</h2>

The <hl>new_from_string</hl> is called a class-method as it is called on the class (My::Date) instead of on a specific instance (e.g. $d).
Compare <hl>My::Date->new_from_string</hl> with <hl>My::Date->new</hl> and with <hl>$d->year</hl>. For the first two, the value on the left side
of the arrow is the name of the class, for the third example, the value on the left hand side of the arrow is the instance object.

When perl runs a method with this arrow notation, it always passes the value from the left hand side of the string as the first parameter.
In the case of the above use of <hl>new</hl> and <hl>new_from_string</hl> this will be the name of the class: <hl>My::Date</hl>

The actual method does not do much. It just splits the string it received and the calls the real constructor with the values in the format it expects them.


<h2>Avoiding calling the class method as object method</h2>

There is nothing in the above code that would stop us from calling the class-method on an instance object like this:

<code lang="perl">
my $d3 = $d2->new_from_string('2001-2-3');
say $d3;
say $d3->year;
</code>

It will even work. Sort of. Incorrectly.

The output of the above code (if pasted at the end of the script earlier this article looks like this:

<code>
Date(2013, 1, 27)
Date(2013, 1, 28)
Date(2013, 1, 28)=HASH(0x7fba51827d58)
Can't locate object method "year" via package "Date(2013, 1, 28)" at bin/date.pl line 19
</code>

The <hl>new_from_string</hl> returned a strange something <hl>Date(2013, 1, 28)=HASH(0x7fba51827d58)</hl>,
but calling <hl>year</hl> really threw an exception.

Part of this strangeness comes from the fact that we have <a href="/stringification-in-classic-perl-oop">overloaded the stringification</a>.
If comment out the 2 lines starting with <hl>use overload</hl> and run the script again we get a different output:

<code>
My::Date=HASH(0x7fb92904f300)
My::Date=HASH(0x7fb928827c38)
Attempt to bless into a reference at ../lib/My/Date.pm line 7.
</code>

The first two lines were the regular string representations of two <hl>My::Date</hl> object. The third line is the exception we got when 
we tried to call <hl>bless</hl> passing a reference instead of a string (the name of the class).

In general we'd better avoid this situation. We can change the <hl>new_from_string</hl> function to make sure the variable <hl>$class</hl>
is a string and not a blessed object.


<code lang="perl">
use Carp qw(croak);
use Scalar::Util qw(blessed);

# YYYY-MM-DD
sub new_from_string {
    my ($class, $date_string) = @_;

    croak("new_from_string must be called on a class-name")
        if blessed $class;

    my ($year, $month, $day) = split /-/, $date_string;
    return $class->new(year => $year, month => $month, day => $day);
}
</code>

<code>
Date(2013, 1, 27)
Date(2013, 1, 28)
new_from_string must be called on a class-name at bin/date.pl line 17.
</code>

It still throws an exception, but this time we decide on the text, and if we used
<hl>croak</hl> instead of <hl>die</hl> the user would even see the line where she
called the <hl>new_from_string</hl> method and not where we detected the problem
inside our class.


