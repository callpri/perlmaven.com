=title Reading from a file in scalar and list context
=timestamp 2015-01-29T17:12:56
=indexes scalar
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

I have previously written about <a href="/scalar-and-list-context-in-perl">scalar and list context in Perl</a>, how
<a href="/the-year-19100">localtime behaves in scalar and list context</a> and what an array returns in scalar context.

This time we'll look at <hl><$fh></hl>, the readline operator of Perl in scalar and list context.

=abstract end

In this article you'll see how the readline operator works in scalar and list context, but if you need
to read in the whole content of a file into a scalar variable or into an array, you might be better off
<a href="/use-path-tiny-to-read-and-write-file">using Path::Tiny</a>. It will make your code look
nicer.

<h2>readline in SCALAR context</h2>

This is the standard example we have already seen several times starting when we
<a href="/open-and-read-from-files">opened a file and read the lines</a>, but let me show it here again:

<code lang="perl">
use strict;
use warnings;
 
my $filename = 'data.txt';
open(my $fh, '<:encoding(UTF-8)', $filename)
  or die "Could not open file '$filename' $!";
 
while (my $row = <$fh>) {
  chomp $row;
  print "$row\n";
}
</code>

The relevant part is that we read from the <hl>$fh</hl> filehandle into a scalar variable: <hl>my $row = <$fh></hl>.
We have already learned that in this case Perl will read one line from the file, up to and including the first new-line
it encounters. Then next time we execute the same expression it will start reading from the next character, meaning
the beginning of the next line.

Compare that with the following example


<h2>readline in LIST context</h2>

<code lang="perl">
use strict;
use warnings;
 
my $filename = 'data.txt';
open(my $fh, '<:encoding(UTF-8)', $filename)
  or die "Could not open file '$filename' $!";
 
my @rows = <$fh>;
chomp @rows;
foreach my $row (@rows) {
  print "$row\n";
}
</code>

In this case, after opening the file we read from the <hl>$fh</hl> filehandle into an array variable: <hl>my @rows = <$fh>;</hl>.
In this case Perl will read in the content of the whole file in one step. Each row in the  file will be one of the elements of
the array. So if the file had 37 rows, then this array will have 37 elements. The first element (index 0) will contain what was
on the first line in the file. The 8th element (index 7) will contain what was on the 8th line. etc.

Each element in the array will include the newline that was at the end of the row. We can get rid of all those newlines in just one statement:
<hl>chomp @rows;</hl> will remove the trailing newline from all of the elements of the array.

Then, instead of using a <a href="/while-loop">while-loop</a> we use a foreach loop to iterate over the elements of the array.



<h2>Which one is better?</h2>

In almost all the cases we would use the first approach, reading the file line-by-line. If we read the whole file into an array this means
we use as much memory as the size of the file. If this is a big file (a few GB-s) then this would probably not work. On the other hand
when we read the file line-by-line as in the first case, we only need to hold in the memory one line at a time.

Nevertheless, there can be cases when it is much easier to process a file when all of it is in memory. For example if we need to
find something that might depend on something later in the file. If you use this approach, please remember to check
the size of the file with the <hl>-s</hl> operator.



<h2>Slurp mode</h2>

Finally there is a third case which is interesting in certain situations especially when you
are trying to find a string that might start on one line and end on a later line.
In that case it can be very useful to have the content of the whole file loaded
into a single scalar variable. This what the Perl developers call <a href="/slurp">slurp mode</a>.
In the referred article you can see examples how to read a file in slurp using core
perl and how to do that using <a href="/use-path-tiny-to-read-and-write-file">Path::Tiny</a>.

