=title Drawing images with Perl using GD::Simple
=timestamp 2014-03-31T22:30:01
=indexes GD, GD::Simple
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

The <a href="https://metacpan.org/pod/GD::Simple">GD</a>  module on CPAN is an interface to the GD Graphics Library.
The Perl module was created and is still maintained by Lincoln D. Stein.

<a href="https://metacpan.org/pod/GD::Simple">GD::Simple</a> is wrapper around the GD module that provides
a simpler interface to create drawings. The following examples are based on the example that comes with the module.

=abstract end

<style>
img.bordered {
  border:solid 1px;
}
</style>

<h2>Red square (new, bgcolor, fgcolor, rectangle, png)</h2>

<code lang="perl">
use strict;
use warnings;
use GD::Simple;
 
# create a new image (width, height)
my $img = GD::Simple->new(200, 100);

# draw a red rectangle with blue borders
$img->bgcolor('red');
$img->fgcolor('blue');
$img->rectangle(10, 10, 50, 50); # (top_left_x, top_left_y, bottom_right_x, bottom_right_y)
                                 # ($x1, $y1, $x2, $y2)

# convert into png data
open my $out, '>', 'img.png' or die;
binmode $out;
print $out $img->png;
</code>

<img src="/img/gd_simple/red_square.png" alt="Red square" class="bordered" />

In these examples a border was added around the image, using CSS, to make it clear what is the size of the actual image.
<hl>my $img = GD::Simple->new(200, 100);</hl> creates the image. It accepts two parameters: <hl>(width, height)</hl>.

The method of the <hl>$img</hl> object are commands to the "drawing head" or "pen". In the above case we set the
background color to red using <hl>bgcolor</hl> and the foreground color (the ink) to be blue using <hl>fgcolor</hl>.

The <hl>rectangle</hl> method will draw a rectangle using the foreground color and filled with the background color.
The 4 parameters the <hl>rectangle</hl> method receives are
<hl>(top left x, top left y, bottom right x, bottom right y)</hl> where the the <hl>(0,0)</hl>
coordinates are in the top left corner. The next example shows this in a clearer way.

The last 3 lines in the above example convert the internal representation to PNG format and save it in
the file called img.png.

<h2>Empty Green Rectangular</h2>

Extend the above example by adding the following code (before saving the image):

<code lang="perl">
# draw an empty rectangle with green borders
$img->bgcolor(undef);
$img->fgcolor('green');
$img->rectangle(20, 40, 100, 60);
</code>

<img src="/img/gd_simple/green_rectangular.png" alt="green rectangular" class="bordered" />

In this example the background color is transparent, so the part that covers the red square show the red square
and the part outside of the red square has white background.  The coordinates provided to the
<hl>rectangle</hl> method also show much clearer where do the values belong.

<h2>Drawing a straight green line (moveTo, lineTo)</h2>

In the next part of the example we will draw a straight line. For this we can use the <hl>lineTo</hl> method that will draw
a line from the current position of the "pen" to the given coordinates <hl>(x, y)</hl> measure from the top left corner of the image.

To position the "pen" to a specific place we use the <hl>moveTo</hl> method providing the <hl>(x, y)</hl> coordinates.

<code lang="perl">
# move to (30, 0) and draw a green line to (100, 40)
$img->moveTo(30, 0);   # (x, y)
$img->lineTo(100, 40); # (x, y)
</code>

<img src="/img/gd_simple/green_line.png" alt="green line" class="bordered" />

<h2>Draw a solid orange ellipse</h2>

In the next step we'll draw an ellipse in the rectangular we drew earlier.
For this first we need to position the "pen" in the center of the rectangular
which is also going to be the center of the ellipse. The rectangular above
has the following coordinates: (20, 40, 100, 60).

The horizontal center is at <hl>(left_x + right_x)/2  = (20+100)/2 = 60</hl>.

The vertical center is at <hl>(top_y + bottom_y)/2  = (40+60)/2 = 50</hl>.

The first thing we do is use <hl>moveTo(60, 50)</hl> to move the "pen".

The total width of the ellipse is <hl>(right_x - left_x) = 100-20 = 80</hl>.

The total height of the ellipse is <hl>(bottom_y - top_y) = 60 - 40 = 20</hl>.

So we draw the ellipse after setting the colors using
<hl>ellipse(80, 20)</hl>.

<code lang="perl">
# draw a solid orange ellipse
$img->moveTo(60, 50);    # (x, y)
$img->bgcolor('orange');
$img->fgcolor('orange');
$img->ellipse(80, 20);   # (width height)
</code>

<img src="/img/gd_simple/orange_ellipse.png" alt="orange ellipse" class="bordered" />

<h2>Draw arc</h2>

An <a href="http://en.wikipedia.org/wiki/Arc_(geometry)">arc</a> is a partial circle or in the more general case
a partial ellipse.

In order to draw one we have to set the "pen" to the starting point of the arc, which is its center, and
provide the following parameters: <hl>($width, $height, $start, $end, $style)</hl>.

If the width and the height are the same then this is part of a circle. 
<hl>$start</hl> and <hl>$end</hl> are degrees from 0 to 360. Zero is at the right hand side of the ellipse
and the degrees grow clock-wise. Hence 0, 90 as given in the example will be the bottom Right quarter.

<code lang="perl">
# draw an arc filled with dark violet
$img->moveTo(100, 50);
$img->bgcolor('darkviolet');
$img->fgcolor('darkviolet');
$img->arc(60, 60, 0, 90, gdEdged);
</code>

<img src="/img/gd_simple/dark_violet_arc.png" alt="dark violet arc" class="bordered" />

<h2>Draw text</h2>

The method <hl>string</hl> can put a string on the image.

<code lang="perl">
# draw a string at (10,30) using the default built-in font
$img->bgcolor('black');
$img->fgcolor('black');
$img->moveTo(10, 30);
$img->string('Perl Maven');
</code>

<img src="/img/gd_simple/text.png" alt="" class="bordered" />

There is of course a lot more you can do with GD and even GD::Simple, but this can get you started.

