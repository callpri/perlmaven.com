=title Command line phonebook with MongoDB and Moo
=timestamp 2015-05-13T07:30:01
=indexes MongoDB, Moo, MooX::Options
=status show
=books mongodb, moo
=author szabgab
=comments_disqus_enable 0

=abstract start

<a href="http://www.mongodb.org/">MongoDB</a> is an open source NoSQL database.
In this example we'll write a small,
command line, phonebook. This is a simple example to see how to
<a href="/mongodb">use MongoDB in Perl</a>.

=abstract end


<h2>Installation</h2>

Before getting started, we'll need to install MongoDB itself
and the <a href="https://metacpan.org/pod/MongoDB">MongoDB Perl driver</a>

On Ubuntu, it means installing both the <hl>mongodb</hl> and
<hl>mongodb-dev</hl> packages.

On OSX I used homebrew to install the mongodb server and then I could
install the Perl driver using cpanm.


<h2>The full example</h2>

<code lang="perl">
use 5.010;
use Moo;
use MooX::Options;

use MongoDB ();
use Data::Dumper qw(Dumper);

option add   => (is => 'ro');
option list  => (is => 'ro');
option name  => (is => 'ro', format => 's');
option phone => (is => 'ro', format => 's');

sub run {
    my ($self) = @_;

    my $client = MongoDB::MongoClient->new(host => 'localhost', port => 27017);
    my $db   = $client->get_database( 'phonebook' );
    my $people_coll = $db->get_collection('people');

    say 'Processing ...';
    if ($self->add) {
        $people_coll->insert( {
            name => $self->name,
            phone => $self->phone,
        });
    } elsif ($self->list) {
        my %query;
        if ($self->name) {
            $query{name} = $self->name;
        }
        if ($self->phone) {
            $query{phone} = $self->phone;
        }
        my $people = $people_coll->find(\%query);
        while (my $p = $people->next) {
            printf "%s  %s\n", $p->{name}, $p->{phone};
        }
    } else {
        die "Missing --add or --list";
    }
}
 
main->new_with_options->run;
</code>


<h2>The command line part</h2>

We are using the same technique explained in the article about
<a href="/command-line-scripts-with-moo">command line scripts using Moo</a>:

We turn the whole script into a Moo-based class by <hl>use Moo;</hl>
Load <hl>use MooX::Options;</hl> to allow us to declare options and
we declare 4 optional attributes. Two of them can accept strings:
<hl>format => 's'</hl> the other two are boolean options. (aka. flags).

The call to <hl>main->new_with_options->run;</hl> processes
the options and then calls the <hl>run</hl> method.

Inside the <hl>run</hl> method we'll have access to the values
provide by the user on the command line. 

Basically we expect to have two types of actions here:

Adding a new person:

<hl>perl phonebook.pl --add --name 'Foo Bar' --phone 1234</hl>

Listing all the people

<hl>perl phonebook.pl --list</hl>

Listing specific people:

<hl>perl phonebook.pl --list --name Foo</hl>

<code lang="perl">
use 5.010;
use Moo;
use MooX::Options;

use Data::Dumper qw(Dumper);

option add   => (is => 'ro');
option list  => (is => 'ro');
option name  => (is => 'ro', format => 's');
option phone => (is => 'ro', format => 's');

sub run {
    my ($self) = @_;
    say $self->name;
    say $self->phone;
}

main->new_with_options->run;
</code>


<h2>Setting up a MongoDB database</h2>

Besides making sure the MongoDB server runs,
and that we can connect to it, there is not much to
do in order to create a database:

<code lang="perl">
use MongoDB ();
my $client = MongoDB::MongoClient->new(host => 'localhost', port => 27017);
my $db   = $client->get_database( 'phonebook' );
my $people_coll = $db->get_collection('people');
</code>

After loading the MongoDB driver with <hl>use MongoDB ();</hl>
we can connect to a running server using the <hl>new</hl>
constructor of the <hl>MongoDB::MongoClient</hl> module.

By default no authentication is required.

Then we fetch our database by providing its name:
<hl>my $db   = $client->get_database( 'phonebook' );</hl>

There is no need to create the database. Basically the first time
we add some data to it, the database will spring to existence.
Just like the <a href="/autovivification">autovivification</a> in Perl.

The last step in this example is to fetch the object representing
the collection that will actually hold the list of people:
<hl>my $people_coll = $db->get_collection('people');</hl>

A collection in MongoDB is similar to a table in an SQL database.

In this case too, we don't need to make any special preparations.
Once we add the first entry, the collection will be created.


<h2>Inserting a document</h2>

If the user supplied the <hl>--add</hl> flag
we take all the other data (in our case that is the name and
the phone), create a hash reference and pass it to the
<hl>insert</hl> method. No need to declare how the table looks like.
In fact the entries don't even need to have the same content.

<code lang="perl">
    if ($self->add) {
        $people_coll->insert( {
            name => $self->name,
            phone => $self->phone,
        });
</code>

In our example we have not checked if the user has supplied the
necessary values and we don't check if the same name or the same
phone number already exists in the database.

<h2>Listing the values</h2>

<code lang="perl">
    } elsif ($self->list) {
        my %query;
        if ($self->name) {
            $query{name} = $self->name;
        }
        if ($self->phone) {
            $query{phone} = $self->phone;
        }
        my $people = $people_coll->find(\%query);
        while (my $p = $people->next) {
            printf "%s  %s\n", $p->{name}, $p->{phone};
        }
</code>

When the user supplied the <hl>--list</hl> flag we build a
query, which is basically just a hash and then call the
<hl>find</hl> method.

This is how we build the query:

<code lang="perl">
    my %query;
    if ($self->name) {
        $query{name} = $self->name;
    }
    if ($self->phone) {
        $query{phone} = $self->phone;
    }
</code>
 

The <hl>find</hl> method will return an <b>iterable</b> object.
This means we can call the <hl>next</hl> method on the object
and it will return the data entries one by one.
When it exhausted all the answers it will return <hl>undef</hl>.
The data entries returned by the <hl>next</hl> call are the
same hash references we supplied to the <hl>insert</hl> method.

<code lang="perl">
    my $people = $people_coll->find(\%query);
    while (my $p = $people->next) {
        printf "%s  %s\n", $p->{name}, $p->{phone};
    }
</code>

That's it. We can now use the the basics of MongoDB.


<ol>
 <li><a href="/phonebook-with-mongodb-and-moo">Command line phonebook with MongoDB and Moo</a> (this article)</li>
 <li><a href="/updating-mongodb-with-perl">Updating MongoDB using Perl</a></li>
</ol>

