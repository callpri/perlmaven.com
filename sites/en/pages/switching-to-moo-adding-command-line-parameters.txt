=title Switching to Moo - adding command line parameters
=timestamp 2015-06-01T10:40:01
=indexes Moo, MooX::Options
=status show
=books moo, mbox
=author szabgab
=comments_disqus_enable 0

=abstract start

Returning to the project after 3 weeks break was not easy. I ran the script again and it finished within a few seconds.
I looked at the log file that only had two lines. What happened?

=abstract end

<h2>Moo-based code</h2>

Oh, right, I still have the code that will limit the processing to 20 messages. I remember, this already bothered
me earlier that I have to change the code in order to do a limited run. So let's make that conditional on a command line parameter.

There is quite a nice way to do this with <a href="/command-line-scripts-with-moo">Moo and MooX::Options</a> so we are going to do that.
Turn the whole script into a <a href="/moo">Moo</a>-based class and then use MooX::Options for the command line.

<code lang="perl">
use Moo;
use MooX::Options;
</code>

Replace <hl>process($path_to_dir);</hl> by 

<code lang="perl">
main->new_with_options->process($path_to_dir);
</code>

And change the first line for the <hl>process</hl> function from <hl>my ($dir) = @_;</hl> to the following code:

<code lang="perl">
my ($self, $dir) = @_;
</code>

to accept both the instance object and the directory.

Then I ran the script again. It was still running and still finishing in a few seconds.

<h2>Command line parameters</h2>

The next step is to start really using <hl>MooX::Options</hl> for processing the command line options.
Currently the script expects exactly one parameter, the path to the mail directory.

We remove the line that gets the parameter from the command line, 
<hl>my $path_to_dir = shift or die "Usage: $0 path/to/mail\n";</hl>
we won't need to do it ourselves any more. 

Instead we add the declaration of the "path" option:

<code lang="perl">
option path    => (is => 'ro', required => 1, format => 's',
    doc => 'path/to/mail');
</code>

Now we don't have the <hl>$path_to_dir</hl> in the main body of the script so we
stop passing it to the <hl>process()</hl> method:

<code lang="perl">
main->new_with_options->process();
</code>

Also inside the <hl>process</hl> method we are now expecting only the instance object
while what we got in the <hl>$dir</hl> variable earlier will be found in the 
<hl>path</hl> attribute of the object:

Replace

<code lang="perl">
sub process {
    my ($self, $dir) = @_;
</code>

by this:

<code lang="perl">
sub process {
    my ($self) = @_;

    my $dir = $self->path;
</code>

If we run the script as <hl>perl bin/mbox-indexer.pl /path/to/mail</hl> we will get the usage message as now we have to
provide the <hl>--path</hl> command line option name and run the script like this:
<hl>perl bin/mbox-indexer.pl --path /path/to/mail</hl>.

And the script still runs. Great!

<h2>Parameter for processing limit</h2>

Now the real thing. We add a command line option called <hl>--limit</hl> that will accept a number. If it is provided,
we will stop processing the mailboxes after the given number of messages.

First thing is that we add a command line option which accepts and integer (<hl>format => 'i'</hl>) and is not required.

<code lang="perl">
option limit   => (is => 'ro', required => 0, format => 'i',
    doc => 'limit number of messages to be processed');
</code>

Then in the main loop we replace the hard coded limit: <hl>exit if $count > 20;</hl>

by this code:

<code lang="perl">
exit if defined $self->limit and $count > $self->limit;
</code>

We only compare <hl>$count</hl> with limit if there was a limit set.

Now if we run the script as above it will take a long time (almost 4 minutes for me)
because it processes all the files. If we want to limit it to the first 20
we also need to pass <hl>--limit 20</hl> on the command line.

