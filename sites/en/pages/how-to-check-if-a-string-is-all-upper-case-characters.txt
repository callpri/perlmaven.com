=title How to check if a string is all upper case (lower case) characters?
=timestamp 2013-11-17T17:30:01
=indexes open, UTF-8, \p{Uppercase}, uc
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

There is function <hl>is_upper</hl> in Perl, so how can we check if a string contains
only upper case characters?

The simple thing is to compare it to the upper-case version of itself:

<code lang="perl">
if ($str eq uc $str) {
    print "All the characters are upper case\n";
}
</code>

but this will also say that "123" us all upper case.

Sometimes this is not what you need.

=abstract end


<h2>At least one upper case letter</h2>

In addition to checking if the upper case version of the string equals to itself,
we might want to make sure that there is at least one upper case letter.
We can use a regex:

<code lang="perl">
if ($str =~ /[A-Z]/) {
    print "There is an upper case letter\n";
}
</code>

This will work with the Latin ABC, but won't match characters like
ÁÉÍÓÖŐÚÜŰ (the upper case versions of áéíóöőúüű)  from the Hungarian ABC,
nor other strange characters like a <a href="http://en.wikipedia.org/wiki/%C3%84">Umlaut Ä</a> (the upper case version of ä).

<h2>At least one upper case Unicode letter</h2>

For that there is <hl>\p{Uppercase}</hl> that will match exactly one upper case
Unicode character. So it will match any of these:
ÄAÁBCDEÉFGHIÍJKLMNOÓÖŐPQRSTUÚÜŰVWXYZ

<code lang="perl">
if ($str =~ /\p{Uppercase}/) {
    print "There is an upper case Unicode letter\n";
}
</code>

<h2>All the characters are upper case Unicode letters</h2>

It might not be enough to have one upper case letter.
You might require to have all of them:
In that case we can use the <hl>^</hl> and <hl>$</hl> regex anchors
to match the beginning and the end of the string respectively.

We also apply the <hl>+</hl> quantifier that means 1 or more of the
preceding thing. In our case one or more of the preceding upper case character.

<code lang="perl">
if ($str =~ /^\p{Uppercase}+$/) {
    print "There is an upper case Unicode letter\n";
}
</code>


<h2>All the characters are upper case Unicode letters or space</h2>

Of course it is rare that a string would only consist of upper case letters.
sometime we also want to allow other characters. For example we would like to allow
spaces as well. In this case we create a character class (in square brackets)
that is built up from a space and the character class representing all the upper
case letters in the world: <hl>[ \p{Uppercase}]</hl>.

<code lang="perl">
if ($str =~ /^[ \p{Uppercase}]+$/) {
    print "There is an upper case Unicode letter\n";
}
</code>


<h2>Trying the examples</h2>

When trying the example we can either read the strings from a file.
In that case we will probably want to
<a href="/open-and-read-from-files">open the file</a> using the 
UTF-8 flag enabled:

<code lang="perl">
open(my $fh, '<:encoding(UTF-8)', $filename)
</code>


On the other hand, if the strings to be compared are in the code,
one needs to add <hl>use utf8;</hl> to the beginning of the script.

In either case it is recommended to change the standard output channels
to use utf-8 with the following:

<code lang="perl">
use open ':std', ':encoding(utf8)';
</code>

Try this example:

<code lang="perl">
use strict;
use warnings;
use 5.010;
use utf8;
use open ':std', ':encoding(utf8)';

foreach my $str ("1", "ä", "äÄ", " Ä", "X", "Á", "É", "Í", "Ö", "Ő", "Ú", "Ü") {
   if ($str =~ /^[ \p{Uppercase}]+$/) {
      say $str;
   } else {
      say "no $str";
   }
} 
</code>

