=title Fetching Pictures from Flickr using Perl
=timestamp 2015-05-06T15:30:01
=indexes Flickr::API
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

<a href="http://flickr.com/">Flickr</a> has tons of pictures and it has an API that allows
you to connect to it. <a href="http://iamcal.com/">Cal Henderson</a> created the
<a href="https://metacpan.org/pod/Flickr::API">Perl client of the Flickr API</a>. We are going to use that to fetch a few pictures.

=abstract end

<img src="/img/flickr.png" />

<h2>Version</h2>

The code of this article was tested with version 1.08 of
<a href="http://metacpan.org/pod/Flickr::API">Flickr::API</a>.

<h2>Getting the API key</h2>

Visit the <a href="http://www.flickr.com/services/">Flickr App Garden</a>,
log in with your Yahoo account and ask for an API key:

Click on <a href="http://www.flickr.com/services/apps/create/">Create an app</a>,
there click on <a href="http://www.flickr.com/services/apps/create/apply/">Request an API Key</a>

You will have to select if it is for non-commercial/SMB,
or if you are big corp/big money.
You'll also need to give a name of an application you are building. And a description.
I selected the non-commercial/SMB option and wrote that I am building examples
<b>how to use the Flickr API in Perl</b>.

As far as I can see you can get as many API keys as you'd like to, so you can build
multiple applications.

Once you applied you'll receive an API-key and an API-secret. For the examples
in this article we'll only need the API-key.

<h2>Set up client</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Flickr::API;
use Data::Dumper qw(Dumper);

my $key = 'PUT_THE_API_KEY_HERE';

my $api = Flickr::API->new({
    key     => $key,
    #secret  => $secret,  # we don't use the secret in this example
    unicode => 1,
});
</code>

Nothing fancy so far.

<h2>Search for pictures</h2>

Let's search for <hl>cake</hl>:

We need to create a
<a href="http://metacpan.org/pod/Flickr::API::Request">Flickr::API::Request</a> object
with all the details of our request, and then pass it to the <hl>execute_request</hl>
method of the <hl>$api</hl>.

<code lang="perl">
my $request = Flickr::API::Request->new({
    method => 'flickr.photos.search',
    args   => {
        text => 'cake',
    },
});
my $response = $api->execute_request($request);
</code>

The <hl>$response</hl> is a
<a href="https://metacpan.org/pod/Flickr::API::Response">Flickr::API::Response</a>
object which is a subclass of
<a href="https://metacpan.org/pod/HTTP::Response">HTTP::Response</a>,
with a few extra keys in the hash: <hl>tree</hl>, <hl>success</hl>,
<hl>error_code</hl>, and <hl>error_message</hl>.

<hl>success</hl> is either <a href="/boolean-values-in-perl">True or False</a>.

The <hl>tree</hl> is the result of parsing by
<a href="http://metacpan.org/module/XML::Parser::Lite::Tree">XML::Parser::Lite::Tree</a>,
which has some strange behavior that makes it a bit difficult to use the module.

Before we go on we should check if the request was successful:

<code lang="perl">
if (not $response->{success}) {
    die "failure";
}
</code>

Then comes the analysis of the data we received in the <hl>tree</hl>.

The Interesting stuff is in the array held by the children subkey of the tree: <hl>$response->{tree}{children}</hl>.

Strangely, the first child does not have any useful content. So we go for the second child
where all the interesting data can be found.

We print out the attributes key of the second child:

<code lang="perl">
print Dumper $response->{tree}{children}[1]{attributes};
</code>

and this is the output we see:

<code>
$VAR1 = {
    'pages' => '41270',
    'perpage' => '100',
    'total' => '4126958',
    'page' => '1'
};
</code>

This means three are a total of 4,126,958 hits for the word <hl>cake</hl>.
The response is divided into 41,270 pages. Each page has 100 results (well, the last
one might have fewer). We received the first page.

Actually we are in control of the page size and which page we get, but because
we have not passed any parameter to the query besides the search string it
used the default which is 100 results per page and fetch the first page.

Using these values we can actually go over all the images.

<h2>Go over the images of the current response</h2>

The following snippet will fetch the list of (100) images from the current response
and then we go over the images:

<code lang="perl">
my @images = grep { $_->{attributes} } @{ $response->{tree}{children}[1]{children} };

foreach my $img (@images) {
    my $attr = $img->{attributes};
    print Dumper $attr;
    exit;
}
</code>

Actually, before going over all the images, let's see what attributes each image
provide in the search result. (That's why we call exit just after the first iteration.
The result is:

<code>
$VAR1 = {
    'farm' => '6',
    'owner' => '100072202@N05',
    'id' => '11050960463',
    'secret' => '7444f5813b',
    'isfamily' => '0',
    'title' => "I made a pretty mean birthday cake for Jeff! So much better than store bought 'cause it's made with LOVE! \x{2665}",
    'ispublic' => '1',
    'server' => '5513',
    'isfriend' => '0'
};
</code>

So from here we could get the title of the image: <hl>$attr->{title};</hl> and a
few other items. We could even construct the URL to one of the copies of the
actual image:

<code lang="perl">
    my $url = sprintf('http://farm%s.staticflickr.com/%s/%s_%s_o.jpg',
        $attr->{farm}, $attr->{server}, $attr->{id}, $attr->{secret});
    say $url;
</code>

<h2>Get the sizes</h2>

Instead of that we can use another query to fetch the available sizes
for this image:

<code lang="perl">
    my $request = Flickr::API::Request->new({
        method => 'flickr.photos.getSizes',
        args   => {
            photo_id => $attr->{id},
        },
    });
    my $resp = $api->execute_request($request);
    if (not $response->{success}) {
        die "failure";
    }
    my @sizes = grep { $_->{attributes} } @{ $resp->{tree}{children}[1]{children} };
    print Dumper $sizes[0];
</code>

This is the data we get for a single size of an image:

<code>
$VAR1 = {
    'type' => 'element',
    'name' => 'size',
    'attributes' => {
        'height' => '75',
        'label' => 'Square',
        'media' => 'photo',
        'url' => 'http://www.flickr.com/photos/100072202@N05/11050960463/sizes/sq/',
        'source' => 'http://farm6.staticflickr.com/5513/11050960463_7444f5813b_s.jpg',
        'width' => '75'
    },
    'children' => []
};
</code>

the values that really interest me are height, width and the source, which is the actual URL to the image.

Now that we have image we can use something as simple as the <hl>getstore</hl> function of
<a href="https://metacpan.org/pod/LWP::Simple">LWP::Simple</a>.

<h2>Putting it all together</h2>

Let's say we would like to download the smallest version of 20 cakes:
(Because the images can come in all kinds of aspect ratios, we go for the smallest
height.)


<code lang="perl">
use strict;
use warnings;
use 5.010;

use Flickr::API;
use Data::Dumper qw(Dumper);
use LWP::Simple qw(getstore);

my $dir = "flickr";
mkdir $dir;

my $key = 'PUT_THE_API_KEY_HERE';

my $api = Flickr::API->new({
    key     => $key,
    unicode => 1,
});

my $request = Flickr::API::Request->new({
    method => 'flickr.photos.search',
    args   => {
        text => 'cake',
        per_page => 20,
        page => 1,
    },
});

my $response = $api->execute_request($request);
if (not $response->{success}) {
    die "failure";
}
my @images = grep { $_->{attributes} } @{ $response->{tree}{children}[1]{children} };
foreach my $img (@images) {
    my $attr = $img->{attributes};
    my $request = Flickr::API::Request->new({
        method => 'flickr.photos.getSizes',
        args   => {
            photo_id => $attr->{id},
        },
    });
    my $resp = $api->execute_request($request);
    if (not $response->{success}) {
        warn "failure";
        next;
    }
    my @sizes = grep { $_->{attributes} } @{ $resp->{tree}{children}[1]{children} };

    # sort the sizes according to height and get the fist one, the smallest
    my ($smallest) = sort { $a->{attributes}{height} <=> $b->{attributes}{height} } @sizes;

    # get the last part of the URL which is the filename
    my ($file) = ($smallest->{attributes}{source} =~ m{([^/]*)$});

    say "$smallest->{attributes}{source}, $dir/$file";
    getstore($smallest->{attributes}{source}, "$dir/$file");
}
</code>

