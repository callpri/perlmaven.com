=title Packages, modules, distributions, and namespaces in Perl
=timestamp 2014-02-17T21:50:01
=indexes package
=status show
=books advanced
=author szabgab
=comments_disqus_enable 0

=abstract start

Seasoned Perl developers freely use words such as <hl>package</hl>, <hl>module</hl>, <hl>distribution</hl>, <hl>symbol-table</hl>,
<hl>release</hl>, and <hl>namespace</hl>.
Sometimes interchangeably.
Sometimes slightly blurring the differences between the words.
This can easily create confusion. Especially among people who have less than 10 years experience with Perl.

Let's try to clear that confusion.

=abstract end

Quick explanation:

<ul>
  <li><b>release</b> usually refers to a zipped file (tar.gz or zip) file uploaded to CPAN (via PAUSE) for example: Some-Thing-1.01.ta.gz</li>
  <li><b>distribution</b> sometimes refers to the same thing as a <b>release</b>, in other cases it can refer to all the Some-Thing releases, regardless of their version number.</li>
  <li><b>namespace</b> is a container of identifiers (variables, functions). A namespace would be Some::Thing.</li>
  <li><b>symbol-table</b> is the place where the identifier of a namespace are stored. Basically we can think of a <b>symbol-table</b> as being equivalent to <b>namespace</b>.</li>
  <li><b>package</b> is a keyword of Perl that switches to a new namespace. Sometimes people refer to a particular <b>release</b> or a <b>distribution</b> as being a <b>package</b>,
       but that only happens because when we zip up several files of a <b>release</b> we often think about the English word <b>package</b>.</li>
  <li><b>module</b> is the name of a package (namespace) that is kept in a file derived from its name. (a package/namespace called Some::Thing kept in a file called Some/Thing.pm is called a module.
      Unfortunately when we say "module" we often refer to a whole distribution.</li>

<h2>Details</h2>

Every piece of Perl code is in a namespace. When we run a simple script like this:

<code lang="perl">
use warnings;

$x = 2;
$
</code>

We will get a warning <a href="/name-used-only-once-possible-typo">Name "main::x" used only once: possible typo at ... line ...</a>.

The <hl>main</hl> in that warning is the namespace of the current script and thus the current variables. It is there implicitly.
We did not need to write anything and we were already in the <hl>main</hl> namespace.

Of course we know we should <a href="/strict">always use strict</a>, and so we add it to the code:

<code lang="perl">
use strict;
use warnings;

$x = 2;
</code>

Now we get the following error: <a href="/global-symbol-requires-explicit-package-name">Global symbol "$x" requires explicit package name at ... line ...</a>.
Here the error mentioned the word <hl>package</hl>. This error usually indicates that we forgot to declare the variable with <hl>my</hl>, but the actual
error message indicates something else. For historical reasons, the error points out another way to use a variable: by providing the name of the package
(the namespace) it resides in.

This works:

<code lang="perl">
use strict;
use warnings;

$main::x = 42;
print "$main::x\n";  # 42
</code>


<code lang="perl">
use strict;
use warnings;

my $x = 23;
$main::x = 42;

print "$main::x\n";  # 42
print "$x\n";        # 23
</code>

This is very confusing. There are now two variables that seem to be called <hl>$x</hl>, but they are actually
two different variables. <hl>$main:x</hl> is a <a href="/package-variables-and-lexical-variables-in-perl">package variable</a>
and <hl>$x</hl> is a <a href="/package-variables-and-lexical-variables-in-perl">lexical variable</a>.

In most cases we will use lexical variables declared with <hl>my</hl> and use namespaces only to separate functions.

<h2>Switching namespace using the package keyword - functions</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

sub hi {
    return "main";
}

package Foo;

sub hi {
    return "Foo";
}

say main::hi();   # main
say Foo::hi();    # Foo
say hi();         # Foo

package main;

say main::hi();   # main
say Foo::hi();    # Foo
say hi();         # main
</code>

In this code we used the <hl>package</hl> keyword to switch from the default <hl>main</hl> namespace to the <hl>Foo</hl> namespace.
We declared the <hl>hi()</hl> function in both namespaces. Each function returns the name of its own namespace.

Then we called them x x 3 times. When the full names were provided the output was consistent with the full name of the function:
<hl>main::hi()</hl> always returns "main", and <hl>Foo::hi()</hl> always returns "Foo". When called <hl>hi()</hl> without the
namespace prefix, it called the function that was local to the current namespace. The first time we called <hl>hi()</hl> we were
in the "Foo" namespace and so it returned "Foo". Then we switched back to the "main" namespace using <hl>package main;</hl> and
then the call to <hl>hi()</hl> returned "main".

<h2>Namespace (package) and modules</h2>

While we can use the <hl>package</hl> keyword as many times as we want in a single file, thus creating lots of namespaces
it usually creates confusion. It is not a recommended practice. (Only use it in special cases.)

There is even a <a href="https://metacpan.org/pod/Perl::Critic">Perl::Critic</a> policy called
<a href="https://metacpan.org/pod/Perl::Critic::Policy::Modules::ProhibitMultiplePackages">Modules::ProhibitMultiplePackages</a>
that can help us catch such cases. Use the following:

<code>
$ perlcritic --single-policy Modules::ProhibitMultiplePackages script.pl 
Multiple "package" declarations at line 19, column 1.  Limit to one per file.  (Severity: 4)
</code>

Read more on how to check for <a href="/perl-critic-one-policy">one policy violation at a time</a>.

We could move the code of the <b>Foo</b> namespace to a file called foo.pl and load it using <hl>require</hl>, but it is very old-school
and we will have to give the path to the file.
It is much better to put the code of the <b>Foo</b> namespace into a file called <hl>Foo.pm</hl>.

The main script then will look like this:

<code lang="perl">
use strict;
use warnings;
use 5.010;

sub hi {
    return "main";
}

use Foo;

say main::hi();   # main
say Foo::hi();    # Foo
say hi();         # main
</code>

Foo.pm will look like this:

<code lang="perl">
package Foo;
use strict;
use warnings;
use 5.010;

sub hi {
    return "Foo";
}

say main::hi();   # main
say Foo::hi();    # Foo
say hi();         # Foo

1;
</code>

Please note, we added a <hl>use Foo;</hl> statement to the main script (after the sub hi declaration),
we added the usual use-statement to the top of the Foo.pm, and we also added <hl>1;</hl> at the end of the Foo.pm file.

The Foo.pm file defining the Foo <hl>namespace</hl> using the <hl>package</hl> keyword is called a <hl>module</hl>.

<h2>Caveat</h2>

In order for the <hl>use Foo;</hl> statement to find the Foo.pm file we have to make sure it is in one of the directories
in <hl>@INC</hl>. The simplest thing is to put Foo.pm in the <hl>current working directory</hl>, the same directory
where we are when we run the script. Otherwise we will get
an <a href="/cant-locate-in-inc">Can't locate Foo.pm in @INC (you may need to install the Foo module) (@INC contains: ...</a>
error and we'll need to
<a href="/how-to-change-inc-to-find-perl-modules-in-non-standard-locations">change @INC</a>.

In this slightly contrived example the <hl>use Foo;</hl> statement needs to come after the <hl>sub hi</hl> declaration in
the main script as the call to <hl>main::hi()</hl> inside Foo.pm will be executed when the file is loaded.
If we had the <hl>use</hl> statement before the <hl>sub hi</hl> statement we would get a run-time exception:

<code>
Undefined subroutine &main::hi called at Foo.pm line 10.
Compilation failed in require at script.pl line 5.
BEGIN failed--compilation aborted at script.pl line 5.
</code>

