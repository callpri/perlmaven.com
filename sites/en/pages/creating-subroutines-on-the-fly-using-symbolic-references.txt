=title Creating subroutines on the fly using Symbolic references
=timestamp 2015-04-16T09:40:01
=indexes strict, symbolic references
=status show
=books advanced
=author szabgab
=comments_disqus_enable 0

=abstract start

If you read the documentation of  <a href="https://metacpan.org/pod/Log::Dispatch">Log::Dispatch</a>,
you will see you can send a message to the logger by using the <hl>log</hl> method passing both the
"log level" and the "message" like this: <hl>$logger->log( level => 'error', message => 'Blah, blah' );</hl>.
For greater convenience you can also write <hl>$logger->error( 'Blah, blah' );</hl>.

There are 8 log levels, and <a href="/dave-rolsky">David Rolsky</a>, the author of the module, lets you
use a few additional names as well. For example he allows the use of <hl>err</hl> instead of <hl>error</hl>.

=abstract end

All together there are 12 such helper function. They are almost identical. How can you create and maintain them,
without actually having 12 copies in your source code?


<h2>Creating 12 almost identical functions</h2>

I'll show you the snippet from an older version of the module (specifically this is from 2.29)
as the newer versions are a bit more complex.

If we have implemented the functions manually we would have written code like this:

<code lang="perl">
sub warning {
    my $self = shift;
    $self->log( level => 'warning', message => "@_" );
};
</code>

12 times. In each case replacing the word <hl>warning</hl> (in three places!) with one of the 11 other words.

The code generating the 12 subroutines is going to be longer and more complex than any one of the individual functions,
but it is shorter than the 12 functions together. The big advantage though is that if you want to make
any changes to all the 12 functions, you need to make it in a single location.
Also if you decide to add another name, you just add a single word
to the list at the top and you are done with it.

Here is how the code looks like:

<code lang="perl">
    foreach my $l (
        qw( debug info notice warn warning err error crit critical alert emerg emergency )
        ) {
        my $sub = sub {
            my $self = shift;
            $self->log( level => $l, message => "@_" );
        };
 
        no strict 'refs';
        *{$l} = $sub;
    }
</code>

How does this work?

This code snippet creates an anonymous function and assigns it to the <hl>$sub</hl> variable.
The value passed to the <hl>level</hl> parameter is baked into the currently generated function.

<code lang="perl">
        my $sub = sub {
            my $self = shift;
            $self->log( level => $l, message => "@_" );
        };
</code>

The following code takes the reference to the subroutine, (in <hl>$sub</hl>) and places it in the current
name-space. 

<code lang="perl">
        *{$l} = $sub;
</code>

The <hl>*{$l}</hl> notation might be a strange, but it is quite similar to the case of scalars.
For example in the article about <a href="/symbolic-reference-in-perl">symbolic references</a>
there was an example <hl>${$name}</hl>. The difference is that for functions we used a <hl>*</hl>.

This is how we add an entry to the symbol table of the current name-space, that holds the
list of all the functions. Because this is a case when we want to use symbolic references
we don't want to have <hl>use strict</hl> stopping us. On the other hand the rest of the code
should work under the restrictions of <hl>use strict</hl>.

Perl allows us to turn strict on/off in a lexical scope - that is within a block enclosed by curly braces.
So we can use <hl>no strict</hl> and it will turn off the influence of <hl>strict</hl> from that point
until the next closing brace.

As <hl>strict</hl> actually has 3 parts, it is enough for us to turn off the part that eliminates symbolic references.
Hence we have in the code <hl>no strict 'refs';</hl>.


<h2>Conclusion</h2>

While it is better to avoid accidental use of <a href="/symbolic-reference-in-perl">symbolic references</a>
and having <hl>use strict;</hl> does this, there are rare cases when an expert like you can gain
extra powers by using them.

For this you'll have to turn off strict, but you will can do it in a very limited scope.

