=title Putting the email in MongoDB - part 1
=timestamp 2015-05-26T10:02:01
=indexes Email::Address, MongoDB
=status show
=books mongodb, mbox
=author szabgab
=comments_disqus_enable 0

=abstract start

Now that we have a some feeling of the fields we'll have to store let's go back to the From-fields, clean it up and store it in our database.

=abstract end

<a href="https://metacpan.org/pod/Email::Address">Email::Address</a>, another module by RJBS, can handle e-mail addresses. It has a method called <hl>parse</hl> that would take a string
and try to find all the e-mail address in that string. So we can pass the value of the From field to it and get back the list of, hopefully one,
address.

I took the script from <a href="/indexing-emails-in-an-mbox">the previous article</a> and added the following code in the internal while-loop, then ran the script:

<code lang="perl">
    my $from_string = $msg->header('From');
    my @from = Email::Address->parse($from_string);
    say $from[0]->address;
    say $from[0]->name;
</code>

It nicely printed out the e-mail addresses and the names of the sender.

<h2>Some data validation</h2>

I am not sure how much I can trust these e-mail boxes and the parser, will there be cases when it
cannot recognize any e-mail addresses and will return an empty list?
Can there be cases when it returns more than one values?
We saw that there are tons of fields, most of them only in some of the messages. While <hl>From</hl> seems to be a
required field, can there be messages without a <hl>From</hl> field?

Let's be defensive here and check for all the possibilities:

<code lang="perl">
    my $from_string = $msg->header('From');
        if (not defined $from_string) {
        warn "There is no From field in this message";
        next;
    }
    my @from = Email::Address->parse($from_string);
    if (@from > 1) {
        warn "Strange, there were more than one emails recognized in the From field: " . $msg->header('From');
    }
    if (not @from) {
        warn "Very strange. No email in the From field! " . $msg->header('From');
        next;
    }
    say $from[0]->address;
    say $from[0]->name;
</code>

In this first version, I just printed a warning and depending on the seriousness either let the loop continue,
or, if the From field was missing or could not be recognized, aborted the current iteration and went for the
<hl>next</hl> message. Later on we'll have to improve this reporting.

<h2>Mail System Internal Data</h2>

As I noticed some (and strangely not all) the mail boxes have one e-mail at the beginning of the
box holding some internal data. Later I'll have to investigate why is the difference, but for now
let's just filter out these messages:

<code lang="perl">
    if ($from[0]->name eq 'Mail System Internal Data') {
        next;
    }
</code>

<h2>Adding to MongoDB</h2>

Now that we have the sender addresses from all the messages, let's start putting the data in MongoDB.

We load the <a href="https://metacpan.org/pod/MongoDB">perl driver to MongoDB</a>, connect to the server using the
<a href="https://metacpan.org/pod/MongoDB::MongoClient">MongoDB::MongoClient</a>.
Then we need to select the database using the <hl>get_database</hl> method.
(In MongoDB we don't need to create a database. It is enough just to add some data to it and
the MongoDB server will automatically create the database as well.)

From the database object we need to select one of the collections. This too, will be automatically created
when we store the first document. As we are going to store the e-mail messages, we'll just call the
collection "messages".

This is what we add at the beginning of the script:

<code lang="perl">
use MongoDB;

my $client     = MongoDB::MongoClient->new(host => 'localhost', port => 27017);
my $database   = $client->get_database( 'mboxer' );
my $collection = $database->get_collection( 'messages' );
</code>

Then inside the internal while-loop, after we have parsed the From field and checked that we got
exactly one address back, we create a <hl>document</hl> that we'll store in MongoDB.

A document in MongoDB is just a Perl hash. So we create:

<code lang="perl">
    my %doc = (
        From => {
            name => $from[0]->name,
            address => $from[0]->address,
        },
    );
</code>

and the store it in the database:

<code lang="perl">
    $collection->insert(\%doc);
</code>

There is no need to do anything else.

We run the script <hl>perl bin/mboxer.pl /home/gabor/mail</hl>, and if everything worked well then it added a few documents to the database.
(Only a few as we still have the <hl>exit if $count > 20;</hl> in the loop to limit the run-time.

<h2>MongoDB client</h2>

Let's check the results using the command-line client of MongoDB:

<code>
$ mongo
> show dbs
dev      0.203125GB
local    0.078125GB
mboxer   0.203125GB

> use mboxer
switched to db mboxer

> show collections
messages
system.indexes

> db.messages.find()
{ "_id" : ObjectId("52ea2aa6fde2dafe02000000"), "From" : { "name" : "Foo Bar", "address" : "foo@bar.com" } }
{ "_id" : ObjectId("52ea2aa6fde2dafe02000001"), "From" : { "name" : "Perti Bar", "address" : "peti@bar.org" } }
...
</code>

So far it looks good but we won't want to use this client every time we want to find an e-mail message.
Let's start to build another script that will help us finding e-mail messages.


<code lang="perl">
use strict;
use warnings;
use 5.010;

use MongoDB;

my $client     = MongoDB::MongoClient->new(host => 'localhost', port => 27017);
my $database   = $client->get_database( 'mboxer' );
my $collection = $database->get_collection( 'messages' );

print "mail-boxer> ";
my $term = <STDIN>;
chomp $term;

my $messages = $collection->find({ 'From.address' => qr/$term/ } );
while (my $m = $messages->next) {
    say '';
    say $m->{From}{name};
    say $m->{From}{address};
}
</code>

In this script the user can type in a string that will be used as a regular expression matching
against the e-mail addresses in the database.
The <hl>find</hl> method receives the search path - the keys in the hash structure separated by dots -
and a regular expression to match against the field. The <hl>next</hl> calls in the iterator will
return the next document as we inserted in the other script.

In addition to the actual data we inserted, each hash also contains a key <hl>_id</hl> holding a
MongoDB::OID object. That's the unique id of the document.

<h2>Adding Subject</h2>

Now that we can store and retrieve The From fields, let's add another field. The <hl>Subject</hl> field
seems to be simple as it is free text. So we change the document we create during the indexing and run the script again.

<code lang="perl">
    my %doc = (
        From => {
            name => $from[0]->name,
            address => $from[0]->address,
        },
        Subject => $msg->header('Subject'),
    );
</code>

We also add line to the client script to display the content of the Subject:

<code lang="perl">
   say $m->{Subject};
</code>

When we try to list the data, we'll notice that every message is stored twice.

That's because the collection was not cleaned before we started to collected the
data.

We can add

<code lang="perl">
$database->drop;
</code>

immediately after we called <hl>get_database</hl>. This will remove the existing content of
the whole database, before we start to populate it again.

<h2>Current version</h2>

The current version of the data collector script looks like this:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Path::Iterator::Rule;
use Email::Folder;
use Email::Address;
use MongoDB;

my $path_to_dir = shift or die "Usage: $0 path/to/mail\n";

my $client     = MongoDB::MongoClient->new(host => 'localhost', port => 27017);
my $database   = $client->get_database( 'mboxer' );
$database->drop;
my $collection = $database->get_collection( 'messages' );

my $count = 0;

my $rule = Path::Iterator::Rule->new;
my $it = $rule->iter( $path_to_dir );
while ( my $file = $it->() ) {
    next if not -f $file;
    say $file;
    my $folder = Email::Folder->new($file);
    while (my $msg = $folder->next_message) {  # Email::Simple objects
        $count++;
        my $from_string = $msg->header('From');
        if (not defined $from_string) {
            warn "There is no From field in this message";
            next;
        }
        my @from = Email::Address->parse($from_string);
        if (@from > 1) {
            warn "Strange, there were more than one emails recognized in the From field: " . $msg->header('From');
        }
        if (not @from) {
            warn "Very strange. No email in the From field! " . $msg->header('From');
            next;
        }
        say $from[0]->address;
        say $from[0]->name;
        if ($from[0]->name eq 'Mail System Internal Data') {
            next;
        }
        my %doc = (
            From => {
                name => $from[0]->name,
                address => $from[0]->address,
            },
            Subject => $msg->header('Subject'),
        );
        $collection->insert(\%doc);
        exit if $count > 20;
    }
}
say $count;
</code>


